/* you should modify this linker script  */
SECTIONS
{
    /*
     * cs107e uses this:
     *      .text 0x8000 :  { start.o(.text*)  *(.text*) } 
     * which makes linking in start.o awkward if you don't copy it into
     * each dir.
     *
     * XXX: I cannot make text.boot work if we link start into libpi.a 
     */
    .hdr 0x8000 : {
        LONG((0xea << 24) | ((0x40 - 8) / 4));
        __hdr_start__ = .;
        BYTE(104);
        BYTE(101);
        BYTE(108);
        BYTE(108);
        BYTE(111);
        BYTE(0);
    }
    .text 0x8040 :  { KEEP(*(.text.boot))  *(.text*) }
    .data : { *(.data*) } 
    .rodata : { *(.rodata*) }
    .bss : {
        __bss_start__ = .;
        *(.bss*)
        *(COMMON)
        . = ALIGN(8);
        __bss_end__ = .;
        . = ALIGN(8);
        __heap_start__ = .;
    }
}
